grammar org.larz.dom3.dm.Dm hidden(ML_COMMENT, SL_COMMENT, WS)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate dm "http://www.larz.org/dom3/dm/Dm"

Dom3Mod:
	(('#modname' modname=STRING) &
	('#description' desc=STRING)? &
	('#icon' icon=STRING)? &
	('#version' version=VersionNum)? &
	('#domversion' domversion=VersionNum)? &
	('#poppergold' poppergold=INT)? &
	('#resourcemult' resourcemult=INT)? &
	('#supplymult' supplymult=INT)? &
	('#unresthalfinc' unresthalfinc=INT)? &
	('#unresthalfres' unresthalfres=INT)? &
	('#eventisrare' eventisrare=INT)? &
	('#turmoilincome' turmoilincome=INT)? &
	('#turmoilevents' turmoilevents=INT)? &
	('#deathincome' deathincome=INT)? &
	('#deathsupply' deathsupply=INT)? &
	('#deathdeath' deathdeath=INT)? &
	('#slothincome' slothincome=INT)? &
	('#slothresources' slothresources=INT)? &
	('#coldincome' coldincome=INT)? &
	('#coldsupply' coldsupply=INT)? &
	('#misfortune' misfortune=INT)? &
	('#luckevents' luckevents=INT)? &
	('#researchscale' researchscale=INT)? &
	('#end')?)
	elements+=AbstractElement*;

AbstractElement:
	 Armor | Weapon | Monster | Spell | Item | Name | Site | Nation;
	
VersionNum:
	INT ('.' INT)*;
	
	
/**************/
/* Armor Mods */
/**************/
Armor:
	SelectArmorById | SelectArmorByName | NewArmor;
	
SelectArmorById:
	'#selectarmor' value=INT
	mods+=ArmorMods*
	'#end';
	
SelectArmorByName:
	'#selectarmor' value=STRING
	mods+=ArmorMods*
	'#end';
	
NewArmor:
	'#newarmor' value=INT
	mods+=ArmorMods*
	'#end';
	
ArmorMods:
	ArmorPattern1 | ArmorPattern2;
	
ArmorPattern1:
	ArmorInst1 value=STRING;

ArmorPattern2:
	ArmorInst2 value=INT;
	
ArmorInst1:
	name?='#name';
	
ArmorInst2:
	type?='#type' |
	prot?='#prot' |
	def?='#def' |
	enc?='#enc' |
	rcost?='#rcost';
	
	
/***************/
/* Weapon Mods */
/***************/
Weapon:
	SelectWeaponById | SelectWeaponByName | NewWeapon ;
	
SelectWeaponById:
	'#selectweapon' value=INT
	mods+=WeaponMods*
	'#end';
	
SelectWeaponByName:
	'#selectweapon' value=STRING
	mods+=WeaponMods*
	'#end';
	
NewWeapon:
	'#newweapon' value=INT
	mods+=WeaponMods*
	'#end';

WeaponMods:
	WeaponPattern1 | WeaponPattern2 | WeaponPattern3 | WeaponPattern4;
	
WeaponPattern1:
	WeaponInst1 value=STRING;

WeaponPattern2:
	WeaponInst2 value=INT;

WeaponPattern3:
	WeaponInst3 value1=INT value2=INT;

WeaponPattern4:
	WeaponInst4;

WeaponInst1:
	name?='#name';
	
WeaponInst2:
	dmg?='#dmg' |
	nratt?='#nratt' |
	att?='#att' |
	def?='#def' |
	len?='#len' |
	range?='#range' |
	ammo?='#ammo' |
	rcost?='#rcost' |
	sound?='#sound' |
	aoe?='#aoe' |
	secondaryeffect?='#secondaryeffect' |
	secondaryeffectalways?='#secondaryeffectalways' |
	explspr?='#explspr';

WeaponInst3:
	flyspr?='#flyspr';

WeaponInst4:
	(twohanded?='#twohanded') |
	(armorpiercing?='#armorpiercing') |
	(armornegating?='#armornegating') |
	(magic?='#magic') |
	(dt_normal?='#dt_normal') |
	(dt_stun?='#dt_stun') |
	(dt_paralyze?='#dt_paralyze') |
	(dt_poison?='#dt_poison' ) |
	(dt_cap?='#dt_cap') |
	(dt_demon?='#dt_demon') |
	(dt_demononly?='#dt_demononly') |
	(dt_holy?='#dt_holy') |
	(dt_magic?='#dt_magic') |
	(dt_small?='#dt_small') |
	(dt_large?='#dt_large') |
	(dt_constructonly?='#dt_constructonly') |
	(dt_raise?='#dt_raise') |
	(mind?='#mind') |
	(cold?='#cold') |
	(fire?='#fire') |
	(shock?='#shock') |
	(poison?='#poison') |
	(bonus?='#bonus') |
	(charge?='#charge') |
	(flail?='#flail') |
	(nostr?='#nostr') |
	(mrnegates?='#mrnegates') |
	(mrnegateseasily?='#mrnegateseasily');
	

/****************/
/* Monster Mods */
/****************/
Monster:
	SelectMonsterById | SelectMonsterByName | NewMonster;

SelectMonsterById:
	'#selectmonster' value=INT
	mods+=MonsterMods*
	'#end';
	
SelectMonsterByName:
	'#selectmonster' value=STRING
	mods+=MonsterMods*
	'#end';
	
NewMonster:
	'#newmonster' value=INT
	mods+=MonsterMods*
	'#end';

MonsterMods:
	MonsterPattern1 | MonsterPattern2 | MonsterPattern3 | MonsterPattern4 | MonsterPattern5 | MonsterPattern6;
	
MonsterPattern1:
	MonsterInst1 value=STRING;

MonsterPattern2:
	MonsterInst2 value=INT;

MonsterPattern3:
	MonsterInst3 value1=INT value2=INT;

MonsterPattern4:
	MonsterInst4;
	
MonsterPattern5:
	MonsterInst5 (value1=STRING | value2=INT);

MonsterPattern6:
	MonsterInst6 (value=INT)?;

MonsterInst1:
	name?='#name' |
	spr1?='#spr1' |
	spr2?='#spr2' |
	descr?='#descr' |
	armor?='#armor';
	
MonsterInst2:	
	speciallook?='#speciallook' |
	ap?='#ap' |
	mapmove?='#mapmove' |
	hp?='#hp' |
	prot?='#prot' |
	size?='#size' |
	ressize?='#ressize' |
	str?='#str' |
	enc?='#enc' |
	att?='#att' |
	def?='#def' |
	prec?='#prec' |
	mr?='#mr' |
	mor?='#mor' |
	gcost?='#gcost' |
	rcost?='#rcost' |
	pathcost?='#pathcost' |
	startdom?='#startdom' |
	eyes?='#eyes' |
	copystats?='#copystats' |
	copyspr?='#copyspr' |
	restrictedgod?='#restrictedgod' |
	shatteredsoul?='#shatteredsoul' |
	coldres?='#coldres' |
	coldres?='#coldresist' |
	fireres?='#fireres' |
	fireres?='#fireresist' |
	poisonres?='#poisonres' |
	shockres?='#shockres' |
	darkvision?='#darkvision' |
	stealthy?='#stealthy' |
	seduce?='#seduce' |
	succubus?='#succubus' |
	beckon?='#beckon' |
	startage?='#startage' |
	maxage?='#maxage' |
	older?='#older' |
	healer?='#healer' |
	startaff?='#startaff' |
	supplybonus?='#supplybonus' |
	uwdamage?='#uwdamage' |
	coldpower?='#coldpower' |
	firepower?='#firepower' |
	stormpower?='#stormpower' |
	darkpower?='#darkpower' |
	springpower?='#springpower' |
	summerpower?='#summerpower' |
	fallpower?='#fallpower' |
	winterpower?='#winterpower' |
	ambidextrous?='#ambidextrous' |
	banefireshield?='#banefireshield' |
	berserk?='#berserk' |
	standard?='#standard' |
	animalawe?='#animalawe' |
	awe?='#awe' |
	fear?='#fear' |
	regeneration?='#regeneration' |
	reinvigoration?='#reinvigoration' |
	fireshield?='#fireshield' |
	iceprot?='#iceprot' |
	poisoncloud?='#poisoncloud' |
	diseasecloud?='#diseasecloud' |
	bloodvengeance?='#bloodvengeance' |
	castledef?='#castledef' |
	siegebonus?='#siegebonus' |
	patrolbonus?='#patrolbonus' |
	pillagebonus?='#pillagebonus' |
	researchbonus?='#researchbonus' |
	forgebonus?='#forgebonus' |
	douse?='#douse' |
	nobadevents?='#nobadevents' |
	incunrest?='#incunrest' |
	spreaddom?='#spreaddom' |
	leper?='#leper' |
	popkill?='#popkill' |
	heretic?='#heretic' |
	itemslots?='#itemslots' |
	nametype?='#nametype';
	
MonsterInst3:	
	magicskill?='#magicskill' |
	custommagic?='#custommagic' |
	magicboost?='#magicboost' |
	gemprod?='#gemprod';
	
MonsterInst4:
	(clear?='#clear') |
	(clearmagic?='#clearmagic') |
	(clearspec?='#clearspec') |
	(female?='#female') |
	(mounted?='#mounted') |
	(holy?='#holy') |
	(animal?='#animal') |
	(undead?='#undead') |
	(demon?='#demon') |
	(magicbeing?='#magicbeing') |
	(stonebeing?='#stonebeing') |
	(inanimate?='#inanimate') |
	(coldblood?='#coldblood') |
	(coldblood?='#coldblooded') |
	(immortal?='#immortal') |
	(blind?='#blind') |
	(unique?='#unique') |
	(immobile?='#immobile') |
	(aquatic?='#aquatic') |
	(amphibian?='#amphibian') |
	(pooramphibian?='#pooramphibian') |
	(flying?='#flying') |
	(stormimmune?='#stormimmune') |
	(sailing?='#sailing') |
	(forestsurvival?='#forestsurvival') |
	(mountainsurvival?='#mountainsurvival') |
	(swampsurvival?='#swampsurvival') |
	(wastesurvival?='#wastesurvival') |
	(illusion?='#illusion') |
	(spy?='#spy') |
	(assassin?='#assassin') |
	(heal?='#heal') |
	(noheal?='#noheal') |
	(neednoteat?='#neednoteat') |
	(ethereal?='#ethereal') |
	(trample?='#trample') |
	(entangle?='#entangle') |
	(eyeloss?='#eyeloss') |
	(horrormark?='#horrormark') |
	(poisonarmor?='#poisonarmor') |
	(inquisitor?='#inquisitor') |
	(noitem?='#noitem') |
	(noleader?='#noleader') |
	(poorleader?='#poorleader') |
	(okleader?='#okleader') |
	(goodleader?='#goodleader') |
	(expertleader?='#expertleader') |
	(superiorleader?='#superiorleader') |
	(nomagicleader?='#nomagicleader') |
	(poormagicleader?='#poormagicleader') |
	(okmagicleader?='#okmagicleader') |
	(goodmagicleader?='#goodmagicleader') |
	(expertmagicleader?='#expertmagicleader') |
	(superiormagicleader?='#superiormagicleader') |
	(noundeadleader?='#noundeadleader') |
	(poorundeadleader?='#poorundeadleader') |
	(okundeadleader?='#okundeadleader') |
	(goodundeadleader?='#goodundeadleader') |
	(expertundeadleader?='#expertundeadleader') |
	(superiorundeadleader?='#superiorundeadleader');

MonsterInst5:
	weapon?='#weapon' |
	onebattlespell?='#onebattlespell' |
	firstshape?='#firstshape' |
	secondshape?='#secondshape' |
	secondtmpshape?='#secondtmpshape' |
	shapechange?='#shapechange' |
	landshape?='#landshape' |
	watershape?='#watershape' |
	forestshape?='#forestshape' |
	plainshape?='#plainshape' |
	domsummon?='#domsummon' |
	domsummon2?='#domsummon2' |
	domsummon20?='#domsummon20' |
	makemonster1?='#makemonster1' |
	makemonster2?='#makemonster2' |
	makemonster3?='#makemonster3' |
	makemonster4?='#makemonster4' |
	makemonster5?='#makemonster5' |
	summon1?='#summon1' |
	summon5?='#summon5';

MonsterInst6:
	heat?='#heat' |
	cold?='#cold';

/**************/
/* Spell Mods */
/**************/
Spell:
	SelectSpellById | SelectSpellByName | NewSpell;
	
SelectSpellById:
	'#selectspell' value=INT
	mods+=SpellMods*
	'#end';
	
SelectSpellByName:
	'#selectspell' value=STRING
	mods+=SpellMods*
	'#end';
	
NewSpell:
	'#newspell'
	mods+=SpellMods*
	'#end';

SpellMods:
	SpellPattern1 | SpellPattern2 | SpellPattern3 | SpellPattern4 |SpellPattern5;
	
SpellPattern1:
	SpellInst1 value=STRING;

SpellPattern2:
	SpellInst2 value=INT;

SpellPattern3:
	SpellInst3 value1=INT value2=INT;

SpellPattern4:
	SpellInst4;

SpellPattern5:
	SpellInst5 (value1=STRING | value2=INT);

SpellInst1:
	name?='#name' |
	descr?='#descr';
	
SpellInst2:
	school?='#school' |
	researchlevel?='#researchlevel' |
	aoe?='#aoe' |
	damage?='#damage' |
	effect?='#effect' |
	fatiguecost?='#fatiguecost' |
	flightspr?='#flightspr' |
	explspr?='#explspr' |
	nreff?='#nreff' |
	range?='#range' |
	precision?='#precision' |
	sound?='#sound' |
	spec?='#spec' |
	restricted?='#restricted';

SpellInst3:
	path?='#path' |
	pathlevel?='#pathlevel';
	
SpellInst4:
	clear?='#clear';
	
SpellInst5:
	copyspell?='#copyspell' |
	nextspell?='#nextspell';

	
/**************/
/* Item Mods */
/**************/
Item:
	SelectItemById | SelectItemByName | NewItem;
	
SelectItemById:
	'#selectitem' value=INT
	mods+=ItemMods*
	'#end';
	
SelectItemByName:
	'#selectitem' value=STRING
	mods+=ItemMods*
	'#end';
	
NewItem:
	'#newitem'
	mods+=ItemMods*
	'#end';

ItemMods:
	ItemPattern1 | ItemPattern2;
	
ItemPattern1:
	ItemInst1 value=STRING;

ItemPattern2:
	ItemInst2 value=INT;

ItemInst1:
	name?='#name' |
	descr?='#descr' |
	armor?='#armor';
	
ItemInst2:
	constlevel?='#constlevel' |
	mainpath?='#mainpath' |
	mainlevel?='#mainlevel' |
	secondarypath?='#secondarypath' |
	secondarylevel?='#secondarylevel' |
	copyspr?='#copyspr' |
	type?='#type' |
	weapon?='#weapon';


/**************/
/* Name Mods */
/**************/
Name:
	SelectName;
	
SelectName:
	'#selectnametype' value=INT
	mods+=NameMods*
	'#end';
	
NameMods:
	NamePattern1 | NamePattern2;
	
NamePattern1:
	NameInst1 value=STRING;

NamePattern2:
	NameInst2;

NameInst1:
	name?='#addname';
	
NameInst2:
	clear?="#clear";


/**************/
/* Site Mods */
/**************/
Site:
	SelectSiteById | SelectSiteByName | NewSite;
	
SelectSiteById:
	'#selectsite' value=INT
	mods+=SiteMods*
	'#end';
	
SelectSiteByName:
	'#selectsite' value=STRING
	mods+=SiteMods*
	'#end';
	
NewSite:
	'#newsite' value=INT
	mods+=SiteMods*
	'#end';

SiteMods:
	SitePattern1 | SitePattern2 | SitePattern3 | SitePattern4;
	
SitePattern1:
	SiteInst1 value=STRING;

SitePattern2:
	SiteInst2 value=INT;

SitePattern3:
	SiteInst3 value1=INT value2=INT;

SitePattern4:
	SiteInst4;

SiteInst1:
	name?='#name';
	
SiteInst2:
	path?='#path' |
	level?='#level' |
	rarity?='#rarity' |
	loc?='#loc' |
	homemon?='#homemon' |
	homecom?='#homecom' |
	mon?='#mon' |
	com?='#com' |
	gold?='#gold' |
	res?='#res' |
	incscale?='#incscale' |
	decscale?='#decscale';
	
SiteInst3:
	gems?='#gems';

SiteInst4:
	clear?='#clear';


/***************/
/* Nation Mods */
/***************/
Nation:
	SelectNation | IndepFlag;
	
SelectNation:
	'#selectnation' value=INT
	mods+=NationMods*
	'#end';
	
IndepFlag:
	'#indepflag' value=STRING;

NationMods:
	NationPattern1 | NationPattern2 | NationPattern3 | NationPattern4 | NationPattern5 ;
	
NationPattern1:
	NationInst1 value=STRING;

NationPattern2:
	NationInst2 value=INT;

NationPattern3:
	NationInst3;

NationPattern4:
	NationInst4 (value1=STRING | value2=INT);

NationPattern5:
	NationInst5 value1=DecimalValue value2=DecimalValue value3=DecimalValue;
	
DecimalValue:
	INT ('.' INT);

NationInst1:
	name?='#name' |
	epithet?='#epithet' |
	descr?='#descr' |
	summary?='#summary' |
	brief?='#brief' |
	flag?='#flag' |
	mapbackground?='#mapbackground' |
	startsite?='#startsite';
	
NationInst2:
	era?='#era' |
	labcost?='#labcost' |
	templecost?='#templecost' |
	templepic?='#templepic' |
	startunitnbrs1?='#startunitnbrs1' |
	startunitnbrs2?='#startunitnbrs2' |
	hero1?='#hero1' |
	hero2?='#hero2' |
	hero3?='#hero3' |
	hero4?='#hero4' |
	hero5?='#hero5' |
	hero6?='#hero6' |
	multihero1?='#multihero1' |
	multihero2?='#multihero2' |
	defmult1?='#defmult1' |
	defmult1b?='#defmult1b' |
	defmult2?='#defmult2' |
	defmult2b?='#defmult2b' |
	idealcold?='#idealcold' |
	castleprod?='#castleprod' |
	domkill?='#domkill' |
	domunrest?='#domunrest' |
	startfort?='#startfort' |
	defaultfort?='#defaultfort' |
	farmfort?='#farmfort' |
	mountainfort?='#mountainfort' |
	forestfort?='#forestfort' |
	swampfort?='#swampfort' |
	uwfort?='#uwfort' |
	deepfort?='#deepfort';

NationInst3:
	clearnation?='#clearnation' |
	clearrec?='#clearrec' |
	clearsites?='#clearsites' |
	uwnation?='#uwnation' |
	bloodnation?='#bloodnation' |
	nopreach?='#nopreach' |
	dyingdom?='#dyingdom' |
	sacrificedom?='#sacrificedom' |
	nodeathsupply?='#nodeathsupply' |
	autoundead?='#autoundead' |
	zombiereanim?='#zombiereanim' |
	horsereanim?='#horsereanim' |
	wightreanim?='#wightreanim' |
	manikinreanim?='#manikinreanim' |
	tombwyrmreanim?='#tombwyrmreanim';
	
NationInst4:
	startcom?='#startcom' |
	startscout?='#startscout' |
	startunittype1?='#startunittype1' |
	startunittype2?='#startunittype2' |
	addrecunit?='#addrecunit' |
	addreccom?='#addreccom' |
	uwunit1?='#uwunit1' |
	uwunit2?='#uwunit2' |
	uwunit3?='#uwunit3' |
	uwunit4?='#uwunit4' |
	uwunit5?='#uwunit5' |
	uwcom1?='#uwcom1' |
	uwcom2?='#uwcom2' |
	uwcom3?='#uwcom3' |
	uwcom4?='#uwcom4' |
	uwcom5?='#uwcom5' |
	defcom1?='#defcom1' |
	defcom2?='#defcom2' |
	defunit1?='#defunit1' |
	defunit1b?='#defunit1b' |
	defunit2?='#defunit2' |
	defunit2b?='#defunit2b';
	
NationInst5:
	color?='#color';


/*************/
/* Terminals */
/*************/
terminal INT returns ecore::EInt: '-'?('0'..'9')+;

terminal STRING	: 
	'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
	"'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|"'") )* "'"; 
			
terminal ML_COMMENT:
	(SL_COMMENT | (' '* '\t'* '\r'? '\n')+) !('#'|'\r'|'\n') !('\r'|'\n')*;

terminal SL_COMMENT: 
	'-' !('0'..'9') !('\n'|'\r')* ('\r'? '\n')?;
	
terminal WS:
	(' '|'\t'|'\r'|'\n')+;